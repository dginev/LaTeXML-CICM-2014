<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet version="2.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
    xpath-default-namespace="http://www.loc.gov/mods/v3">
    <!-- suitable for output generated by bibutils 
    (http://www.scripps.edu/~cdputnam/software/bibutils)

    Usage e.g.
    $ bib2xml kwarc.bib | saxon - mybib.xsl | xml2bib > my.bib

    Note: this format is different from what we use for generating the 
    homepage.  bibutils has the advantage that it can also convert XML to 
    BibTeX, whereas the other bib2xml used for the homepage has the advantage
    that it preserves unknown fields (such as our pubs field).
    -->

    <!-- the person to search for -->
    <xsl:param name="given" select="'Christoph'"/>
    <xsl:param name="family" select="'Lange'"/>

    <!-- discard all non-matching entries -->
    <xsl:template match="mods[not(name[
        namePart[@type eq 'family'][text() eq $family] and
        namePart[@type eq 'given'][text() eq $given]
        ])]"/>

    <!-- copy everything else -->
    <xsl:template match="node()|@*">
        <xsl:copy>
            <xsl:apply-templates select="node()|@*"/>
        </xsl:copy>
    </xsl:template>
</xsl:stylesheet>

